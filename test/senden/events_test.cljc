(ns senden.events-test
  "Exercises senden re-frame events/subs on the JVM mini runtime."
  (:require [clojure.test :refer [deftest is use-fixtures]]
            [shitsuke.re-frame.core :as rf]
            [senden.events :as events]
            [senden.campaign :as campaign]
            [senden.funnel :as funnel]))

(use-fixtures :each
  (fn [t] (rf/clear!) (events/register!) (rf/dispatch [:senden/init]) (t) (rf/clear!)))

(deftest init-test
  (is (= [] @(rf/subscribe [:senden/campaigns])))
  (is (= 0 (funnel/stage-count @(rf/subscribe [:senden/funnel]) :awareness))))

(deftest campaign-lifecycle-via-events-test
  (rf/dispatch [:campaign/add (campaign/campaign {:id "c1" :name "n" :channel :social})])
  (rf/dispatch [:campaign/transition "c1" :scheduled])
  (rf/dispatch [:campaign/transition "c1" :running])
  (is (= :running (:status (first @(rf/subscribe [:senden/campaigns])))))
  (rf/dispatch [:campaign/spend "c1" 50000])
  (rf/dispatch [:campaign/convert "c1" 25])
  (is (= 50000 (get-in (first @(rf/subscribe [:senden/campaigns])) [:metrics :spend])))
  (is (= 25 (get-in (first @(rf/subscribe [:senden/campaigns])) [:metrics :conversions]))))

(deftest funnel-via-events-test
  (rf/dispatch [:funnel/set :awareness 1000])
  (rf/dispatch [:funnel/set :interest 300])
  (is (= 1000 (funnel/stage-count @(rf/subscribe [:senden/funnel]) :awareness)))
  (is (= 300/1000 (funnel/conversion-rate @(rf/subscribe [:senden/funnel]) :awareness))))
